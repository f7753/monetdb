dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/common/sql.h)
AC_CONFIG_AUX_DIR(conf)
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AH_TOP([#include "sysdefs.h"])

dnl ----------------------
AM_INIT_AUTOMAKE("sql", 2.0, [no-define])
AM_CONFIG_HEADER(sql_config.h:conf/config.h.in)

HOST=[$target]
AC_DEFINE_UNQUOTED(HOST, "$HOST", [Host identifier])

dnl Check for Monet
AM_MONET_COMPILER()
AM_MONET_TOOLS()
AM_MONET_OPTIONS()
AM_MONET_LIBS()
AM_MONET_CLIENT(4.3.6)
if test "x$have_monet" != xyes; then
	AC_MSG_ERROR(Monet is not found.)
fi

AC_CHECK_HEADERS(termios.h) 

dnl libodbc
have_odbc=auto
ODBC_LIBS=""
ODBC_INCS=""
AC_ARG_WITH(odbc,
[  --with-odbc=DIR     odbc library is installed in DIR], 
	[have_odbc="$withval"])
if test "x$have_odbc" != xno; then
  if test "x$have_odbc" != xauto; then
    ODBC_LIBS="-L$withval/lib"
    ODBC_INCS="-I$withval/include"
  fi

  save_CPPFLAGS="$CPPFLAGS"
  CPPFLAGS="$CPPFLAGS $ODBC_INCS"
  AC_CHECK_HEADERS(sql.h)
  CPPFLAGS="$save_CPPFLAGS"

  save_LDFLAGS="$LDFLAGS"
  LDFLAGS="$ODBC_LIBS $LDFLAGS"
  AC_CHECK_LIB(odbc, SQLConnect, 
	[ ODBC_LIBS="$ODBC_LIBS -lodbc" 
          AC_DEFINE(HAVE_LIBODBC, 1, [Define if you have SQLConnect in -lodbc]) 
	  have_odbc=yes ]
	, have_odbc=no )
  LDFLAGS="$save_LDFLAGS"

  if test "x$have_odbc" != xyes; then
    ODBC_LIBS=""
    ODBC_INCS=""
  fi
fi
AM_CONDITIONAL(HAVE_ODBC,test "x$have_odbc" = xyes)
AC_SUBST(ODBC_LIBS)
AC_SUBST(ODBC_INCS)
