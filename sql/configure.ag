dnl Process this file with autoconf to produce a configure script.
AC_INIT(src/common/sql.h)
AC_CONFIG_AUX_DIR(conf)

dnl ----------------------
AM_INIT_AUTOMAKE("sql", 2.0)
AM_CONFIG_HEADER(sql_config.h:conf/config.h.in)
AC_CANONICAL_HOST

HOST=[$host]
AC_DEFINE_UNQUOTED(HOST, "$HOST", [Host identifier])

dnl some dirty hacks
thread_safe_flag_spec="-D_REENTRANT"
[ case "$host_os" in
  solaris*)
    case "$CC" in
      cc*) 
	echo "$CC=cc"
        thread_safe_flag_spec="-mt" ;;
      *) ;;
    esac
    LEXLIB=-ll
    ;;
  irix*)
    LEXLIB=-ll
    ;;
  aix*)
    thread_safe_flag_spec="-D_THREAD_SAFE $thread_safe_flag_spec"
    ;;
esac ]

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_CPP
AC_PROG_GCC_TRADITIONAL
AC_PROG_INSTALL
AM_PROG_LEX
AC_PROG_YACC
AC_DISABLE_STATIC
AC_ENABLE_SHARED
AM_PROG_LIBTOOL
dnl AC_PROG_CC_STDC

AC_PROG_LN_S
AC_CHECK_PROG(RM,rm,rm -f)
AC_CHECK_PROG(MV,mv,mv -f)
AC_CHECK_PROG(LOCKFILE,lockfile,lockfile,echo)

dnl to shut up automake (.m files are used for mel not for objc)
AC_CHECK_TOOL(OBJC,objc)

#AM_DEPENDENCIES(CC)
#AM_DEPENDENCIES(CXX)

dnl Checks for header files.
AC_HEADER_STDC

AM_MONET
AM_MONET_OPTIONS
AM_MONET_LIBS

dnl libodbc
have_odbc=auto
ODBC_LIBS=""
ODBC_INCS=""
AC_ARG_WITH(odbc,
[  --with-odbc=DIR     odbc library is installed in DIR], 
	[have_odbc="$withval"])
if test "x$have_odbc" != xno; then
  if test "x$have_odbc" != xauto; then
    ODBC_LIBS="-L$withval/lib"
    ODBC_INCS="-I$withval/include"
  fi

  save_CPPFLAGS="$CPPFLAGS"
  CPPFLAGS="$CPPFLAGS $ODBC_INCS"
  AC_CHECK_HEADERS(sql.h)
  CPPFLAGS="$save_CPPFLAGS"

  save_LDFLAGS="$LDFLAGS"
  LDFLAGS="$ODBC_LIBS $LDFLAGS"
  AC_CHECK_LIB(odbc, SQLConnect, 
	[ ODBC_LIBS="$ODBC_LIBS -lodbc" 
          AC_DEFINE(HAVE_LIBODBC) 
	  have_odbc=yes ]
	, have_odbc=no )
  LDFLAGS="$save_LDFLAGS"

  if test "x$have_odbc" != xyes; then
    ODBC_LIBS=""
    ODBC_INCS=""
  fi
fi
AM_CONDITIONAL(HAVE_ODBC,test "x$have_odbc" = xyes)
AC_SUBST(ODBC_LIBS)
AC_SUBST(ODBC_INCS)

SHARED_LIBS=''
[
if [ "$enable_static" = "yes" ]; then
	CFLAGS="$CFLAGS -DSTATIC"
	SHARED_LIBS='$(smallTOC_SHARED_LIBS)'
	case "$host_os" in
	*)	CFLAGS="$CFLAGS -DsmallTOC -DSQLmodules";;
#	aix*)	CFLAGS="$CFLAGS -DsmallTOC";;
#	*)	SHARED_LIBS="$SHARED_LIBS "'$(largeTOC_SHARED_LIBS)';;
	esac
	if test "x$GCC" != xyes; then
		case "$host_os" in
		irix*)	SHARED_LIBS="$SHARED_LIBS -lm";;
		linux*) CFLAGS="$CFLAGS -O0";;
		esac
	fi
  else
	if test "x$GCC" != xyes; then
		case "$host_os" in
		linux*) CFLAGS="$CFLAGS -O0";;
		esac
	fi
fi
]
AC_SUBST(SHARED_LIBS)
AM_CONDITIONAL(LINK_STATIC,test "x$enable_static" = xyes)

dnl Checks for library functions.
AC_SUBST(thread_safe_flag_spec)
AC_SUBST(CFLAGS)
AC_SUBST(CXXFLAGS)
