dnl -*- mode: autoconf -*-

# The contents of this file are subject to the MonetDB Public
# License Version 1.0 (the "License"); you may not use this file
# except in compliance with the License. You may obtain a copy of
# the License at
# http://monetdb.cwi.nl/Legal/MonetDBLicense-1.0.html
#
# Software distributed under the License is distributed on an "AS
# IS" basis, WITHOUT WARRANTY OF ANY KIND, either express or
# implied. See the License for the specific language governing
# rights and limitations under the License.
#
# The Original Code is the Monet Database System.
#
# The Initial Developer of the Original Code is CWI.
# Portions created by CWI are Copyright (C) 1997-2005 CWI.
# All Rights Reserved.

dnl Process this file with autoconf to produce a configure script.
AC_PREREQ(2.53)
AC_INIT(src/utils/Mx/Mx.h)
AC_CONFIG_AUX_DIR(conf)
AC_CANONICAL_HOST
AC_CANONICAL_TARGET
AH_TOP([#include "sysdefs.h"])
AH_BOTTOM([#include "stddef.h"])

dnl ----------------------
AM_INIT_AUTOMAKE("MonetDB", 4.7.3)
AM_CONFIG_HEADER(monetdb_config.h:conf/config.h.in)
CONFIG_H=monetdb_config.h
AC_SUBST(CONFIG_H)

HOST=[$target]
AC_DEFINE_UNQUOTED(HOST, "$HOST", [Host identifier])

if test x"$target_os" = xcygwin; then
	AC_DEFINE(WIN32, 1, [Define on MS Windows (also under Cygwin)])
fi

# MONETDB_BUILD and MONETDB_SOURCE are *only* used for monetdb-config
# MONETDB_SOURCE is the absolute path name of the source directory
# (srcdir can be relative)

# The Q versions of various variables are used in places where \'s
# need to be escapes with an extra \.  Configure does not support \'s
# in path names, so there is no need to do anything special here
# except set the variables.  The command to set them in case we do
# need to escape the \'s is Qvar=`echo "$var" | sed 's/\\\\/\\\\\\\\/g'`
MONETDB_BUILD=[`pwd`]
AC_SUBST(MONETDB_BUILD)
QMONETDB_BUILD="$MONETDB_BUILD"
AC_SUBST(QMONETDB_BUILD)
MONETDB_SOURCE=[`(cd $srcdir && pwd)`]
AC_SUBST(MONETDB_SOURCE)
QMONETDB_SOURCE="$MONETDB_SOURCE"
AC_SUBST(QMONETDB_SOURCE)

# if no --prefix option, we must set Qprefix to the default value
# the problem is that prefix is set very late in the process if it
# wasn't set by --prefix
if test "${prefix}x" = "NONEx"; then
	Qprefix="${ac_default_prefix}"
else
	Qprefix="${prefix}"
fi
AC_SUBST(Qprefix)
# exec_prefix is created very late, so let make do the expansion
if test "${exec_prefix}x" = "NONEx"; then
	Qexec_prefix='${prefix}'
else
	Qexec_prefix="${exec_prefix}"
fi
AC_SUBST(Qexec_prefix)
Qsrcdir="$srcdir"
AC_SUBST(Qsrcdir)
Qlocalstatedir="$localstatedir"
AC_SUBST(Qlocalstatedir)
Qlibdir="$libdir"
AC_SUBST(Qlibdir)
Qsysconfdir="$sysconfdir"
AC_SUBST(Qsysconfdir)
Qbindir="$bindir"
AC_SUBST(Qbindir)
Qlibexecdir="$libexecdir"
AC_SUBST(Qlibexecdir)
Qdatadir="$datadir"
AC_SUBST(Qdatadir)
Qmandir="$mandir"
AC_SUBST(Qmandir)
Qincludedir="$includedir"
AC_SUBST(Qincludedir)
Qinfodir="$infodir"
AC_SUBST(Qinfodir)

AM_MONETDB_COMPILER()
AM_MONETDB_TOOLS()
AM_MONETDB_OPTIONS()
AM_MONETDB_LIBS()

DIRSEP="/"
case "$target_os" in
mac*)	DIRSEP=":"
	AC_ERROR([mac not supported yet])
	;;
esac
AC_SUBST(DIRSEP)
QDIRSEP="$DIRSEP"
AC_SUBST(QDIRSEP)

dnl AC_CHECK_PROG(CP,cp,cp -f)
dnl AC_CHECK_PROG(MKDIR,mkdir,mkdir -p)
dnl AC_CHECK_PROG(TAGS,ctags,ctags)

AC_C_CONST()
AC_C_INLINE()

dnl get monet compiler options (debug,warning,optimize,profiling,instrument)

dnl Checks for header files.
AC_HEADER_STDC()
AC_HEADER_DIRENT()
AC_HEADER_SYS_WAIT()
AC_CHECK_HEADERS(fcntl.h limits.h unistd.h getopt.h malloc.h pwd.h dlfcn.h)
AC_CHECK_HEADERS(time.h sys/time.h utime.h sys/utime.h)
AC_CHECK_HEADERS(sys/file.h sys/param.h sys/times.h sys/mman.h)
AC_CHECK_HEADERS(rlimit.h netdb.h sys/resource.h)
AC_CHECK_HEADERS(string.h strings.h signal.h sys/socket.h sys/un.h termios.h)
AC_CHECK_HEADERS(ieeefp.h winsock.h)
AC_CHECK_HEADERS(gc.h)
AC_LANG_SAVE
#AC_LANG_PUSH(C++)
AC_LANG_CPLUSPLUS
AC_CHECK_HEADER(new, AC_DEFINE(HAVE_NEW,1,[Define to 1 if you have the <new> header file.]),,[ ])
AC_CHECK_HEADER(iostream, AC_DEFINE(HAVE_IOSTREAM,1,[Define to 1 if you have the <iostream> header file.]),,[ ])
AC_CHECK_HEADER(cstdio, AC_DEFINE(HAVE_CSTDIO,1,[Define to 1 if you have the <cstdio> header file.]),,[ ])
#AC_LANG_POP(C++)
AC_LANG_RESTORE
dnl We don't need to check for these two: they're only present on
dnl Windows.  We list them here for completeness, since the
dnl corresponding defines are used.
dnl AC_CHECK_HEADERS(io.h winsock.h)

if test -z "$ac_includes_default"	# compatibility with autoconf 2.13
then
	ac_includes_default='#include <stdio.h>
#if HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#if HAVE_SYS_STAT_H
# include <sys/stat.h>
#endif
#if STDC_HEADERS
# include <stdlib.h>
# include <stddef.h>
#else
# if HAVE_STDLIB_H
#  include <stdlib.h>
# endif
#endif
#if HAVE_STRING_H
# if !STDC_HEADERS && HAVE_MEMORY_H
#  include <memory.h>
# endif
# include <string.h>
#endif
#if HAVE_STRINGS_H
# include <strings.h>
#endif
#if HAVE_INTTYPES_H
# include <inttypes.h>
#else
# if HAVE_STDINT_H
#  include <stdint.h>
# endif
#endif
#if HAVE_UNISTD_H
# include <unistd.h>
#endif'
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_PID_T()
AC_TYPE_OFF_T()
AC_TYPE_SIZE_T()
AC_CHECK_TYPES([ptrdiff_t, ssize_t],,,[#include <stddef.h>
#include <sys/types.h>])
AC_CHECK_TYPE(socklen_t, AC_DEFINE(HAVE_SOCKLEN_T, 1, [Define to 1 if the system has the type `socklen_t'.]), , [#include <sys/types.h>
#include <sys/socket.h>])
AC_HEADER_STAT()
AC_HEADER_TIME()
AC_STRUCT_TM()
AC_STRUCT_TIMEZONE()
AC_C_BIGENDIAN()
AC_CHECK_TYPES(long long)
AC_CHECK_TYPES(__int64)
AC_CHECK_SIZEOF(char)
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_CHECK_SIZEOF(void *)
AC_CHECK_SIZEOF(size_t)
AC_CHECK_SIZEOF(ptrdiff_t,,[#include <stddef.h>
#include <sys/types.h>])
AC_CHECK_SIZEOF(long long)
AC_CHECK_SIZEOF(__int64)

AC_TRY_COMPILE([$ac_includes_default
#include <malloc.h>], [struct mallinfo m;], AC_DEFINE(HAVE_STRUCT_MALLINFO, 1, [Define if you have struct mallinfo]))
AC_CACHE_CHECK(for _sys_siglist, mn_cv_have__sys_siglist, [
AC_TRY_LINK([$ac_includes_default], [printf("%s\n", _sys_siglist[0]);], mn_cv_have__sys_siglist=yes, mn_cv_have__sys_siglist=no)])
if test $mn_cv_have__sys_siglist = yes; then
	AC_DEFINE(HAVE__SYS_SIGLIST, 1, [Define if you have _sys_siglist])
fi

AC_COMPILE_IFELSE(AC_LANG_SOURCE([int foo(int * restrict p) { return *p; }]),
AC_DEFINE(HAVE_RESTRICT, 1, [Define if the compiler supports the restrict keyword]),
AC_COMPILE_IFELSE(AC_LANG_SOURCE([int foo(int * __restrict__ p) { return *p; }]),
AC_DEFINE(HAVE___RESTRICT__, 1, [Define if the compiler supports the __restrict__ keyword])))

dnl Checks for library functions.
AC_FUNC_ALLOCA()
AC_FUNC_MMAP()
dnl  autoconf says, we cannot use AC_FUNC_SETPGRP when cross-compiling...
if test x$cross_compiling != xyes; then
	AC_FUNC_SETPGRP()
fi
AC_TYPE_SIGNAL()
AC_FUNC_UTIME_NULL()
AC_FUNC_VPRINTF()
AC_FUNC_MEMCMP()
AC_CHECK_FUNCS(strsignal)
AC_CHECK_FUNCS(mkdir rmdir getcwd getopt getrlimit vsnprintf snprintf kill pipe)
AC_CHECK_FUNCS(strcspn strdup strstr strtod strtof strtol strerror strcasecmp strncasecmp)
AC_CHECK_FUNCS(strtoll strtoull)
AC_CHECK_FUNCS(gethostname gettimeofday setenv putenv select times)
AC_CHECK_FUNCS(fstat getpwuid getuid ftime sysconf fcntl shutdown)
AC_CHECK_FUNCS(mrand48 drand48)
AC_CHECK_FUNCS(nanosleep lockf)
AC_CHECK_FUNCS(ctime_r asctime_r localtime_r isinf fpclassify fpclass getlogin)
AC_CHECK_FUNCS(madvise posix_madvise posix_fadvise uname)
AC_CHECK_FUNCS(opendir readdir rewinddir closedir ftruncate)
AC_CHECK_DECLS([strdup, strtof])
AC_CHECK_DECLS([strtoll, strtoull])
AC_CHECK_DECLS([ctime_r])

asctime_r3=yes
AC_MSG_CHECKING(asctime_r3)
AC_TRY_LINK([$ac_includes_default
#include <time.h>], 
[char buf[26]; struct tm tm; asctime_r(&t,buf,26);],
[AC_DEFINE(HAVE_ASCTIME_R3, 1, [Define if you have asctime_r(struct tm*,char *buf,size_t s)])], [asctime_r3=no])
AC_MSG_RESULT($asctime_r3)

ctime_r3=yes
AC_MSG_CHECKING(ctime_r3)
AC_TRY_LINK([$ac_includes_default
#include <time.h>], 
[char buf[26]; time_t t; ctime_r(&t,buf,26);],
[AC_DEFINE(HAVE_CTIME_R3, 1, [Define if you have ctime_r(time_t*,char *buf,size_t s)])], [ctime_r3=no])
AC_MSG_RESULT($ctime_r3)

AC_SYS_LARGEFILE()

AC_ARG_WITH(mx,
	AC_HELP_STRING([--with-mx=FILE], [Mx is installed as FILE]),
	have_mx="$withval",
	have_mx=auto)
if test "x$have_mx" = auto -a x$cross_compiling = xyes; then
	dnl  When cross-compiling, we need to have MEL/Mx in our PATH that work on the building host.
	AC_PATH_PROGS(MX, Mx Mx$EXEEXT,,$PATH)
	if test x$MX = x; then
		AC_ERROR([No Mx$EXEEXT found in PATH=$PATH])
	fi
elif test "x$have_mx" = auto; then
	MX="$MONETDB_BUILD/src/utils/Mx/Mx$EXEEXT"
else
	MX="$withval"
fi
AC_SUBST(MX)
AM_CONDITIONAL(COMPILE_MX, test x$cross_compiling != xyes -a "x$have_mx" = auto)

AC_ARG_WITH(mel,
	AC_HELP_STRING([--with-mel=FILE], [mel is installed as FILE]),
	have_mel="$withval",
	have_mel=auto)
if test "x$have_mel" = auto -a x$cross_compiling = xyes; then
	dnl  When cross-compiling, we need to have mel in our PATH that work on the building host.
	AC_PATH_PROGS(MEL, mel mel$EXEEXT,,$PATH)
	if test x$MEL = x; then
		AC_ERROR([No mel$EXEEXT found in PATH=$PATH])
	fi
elif test "x$have_mel" = auto; then
	MEL="$MONETDB_BUILD/src/mel/mel$EXEEXT"
else
	MEL="$withval"
fi
AC_SUBST(MEL)
AM_CONDITIONAL(COMPILE_MEL, test x$cross_compiling != xyes -a "x$have_mel" = auto)

if test x$cross_compiling != xyes; then
AM_CONDITIONAL(NOT_WIN32, true)
else
case "$host_os" in
*mingw*)
AM_CONDITIONAL(NOT_WIN32, false);;
*)
AM_CONDITIONAL(NOT_WIN32, true);;
esac
fi

dnl  C[XX]FLAGS for our code are stricter than what autoconf can cope with.
CFLAGS="$CFLAGS \$(X_CFLAGS)"
CXXFLAGS="$CXXFLAGS \$(X_CXXFLAGS)"

